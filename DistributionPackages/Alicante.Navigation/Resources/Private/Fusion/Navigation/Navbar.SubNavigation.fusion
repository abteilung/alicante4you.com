prototype(Alicante.Navigation:Component.Navbar.SubNavigation) < prototype(Neos.Fusion:Component) {
    
  @if.isProduct = ${ q(documentNode).is('[instanceof Abte.Site:Document.Product]')}

  subMenuItems = Neos.Fusion:DataStructure {
    features = Neos.Fusion:DataStructure {
      label = ${ I18n.translate('Abte.Site:Main:features') }
      anchor = 'features'
    }
    description = Neos.Fusion:DataStructure {
      label = ${ I18n.translate('Abte.Site:Main:description') }
      anchor = 'description'
    }
    floorPlans = Neos.Fusion:DataStructure {
      label = ${ I18n.translate('Abte.Site:Main:floorPlans') }
      anchor = 'floor plans'
      @if.hasFloorPlans = ${ q(node).property('floorPlans') }
    }
    location = Neos.Fusion:DataStructure {
      label = ${ I18n.translate('Abte.Site:Main:location') }
      anchor = 'location'
    }
    distances = Neos.Fusion:DataStructure {
      label = ${ I18n.translate('Abte.Site:Main:distances') }
      anchor = 'distances'
      @if.hasDistance=${ q(node).property('atm') || q(node).property('train') || q(node).property('bus') || q(node).property('postbox') || q(node).property('road') || q(node).property('school') }      
    }
  }

  @context {
    subMenuItems = ${ this.subMenuItems }
  }

  menuItems = Neos.Fusion:Map {
    items = ${ subMenuItems }
    itemRenderer = ${ item }
  }

  renderer = afx`
    <nav 
      @if.hasItems={props.menuItems}
      class="bg-opacity-90 bg-neutral subNavigation"
    >
      <div class="container flex flex-row py-2 space-x-6 gridPadding-x">
        <Neos.Fusion:Loop items={props.menuItems} iterationName="i">
          <Alicante.Navigation:Navbar.LinkItem anchorLinks={true} anchor={Carbon.String.urlize(item.anchor)} title={item.label} />
        </Neos.Fusion:Loop>
      </div>
    </nav>
  `
}