prototype(Alicante.Navigation:Component.Extra.Navbar.Navigation) < prototype(Neos.Neos:ContentComponent) {
    
    menuItems = Neos.Neos:MenuItems {
        itemCollection = ${ q(site).property('extraItems') ? q(site).property('extraItems') : null }
        maximumLevels = 1
        renderHiddenInIndex = ${ true }
    }

    phone = ${ q(site).property('helpdeskPhone') }

    renderer = afx`
      <div class="extras bg-base-100">
        <div class="container flex items-center justify-between h-8 md:h-10 xl:h-12 gridPadding-x">
          <nav 
            class="flex items-center text-sm xl:text-base md:space-x-6"
          >
            <Neos.Fusion:Loop items={props.menuItems} iterationName="i">
              <Alicante.Navigation:Navbar.LinkItem />
            </Neos.Fusion:Loop>
            <div "@click.prevent"="showModal = !showModal" class="cursor-pointer text-base-300 hover:text-black">
              { I18n.translate('Alicante.Navigation:Main:contact') }
            </div>
          </nav>
          <div>
            <Carbon.Link:Link 
                additionalClass="font-bold text-black hover:underline" 
                link={'tel:' + String.replace(String.pregReplace(props.phone, "'/[^0-9]/'", ''), ' ', '')} 
                renderDefaultTagIfNoLink='true'
            >
              <Neos.Neos:Editable property="helpdeskPhone" node={site} />
            </Carbon.Link:Link>
          </div>
        </div>  
      </div>

    `
}