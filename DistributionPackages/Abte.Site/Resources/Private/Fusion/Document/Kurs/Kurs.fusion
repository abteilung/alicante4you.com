prototype(Abte.Site:Document.Kurs) < prototype(Abte.Site:Document.AbstractPage) {

    body.content = Neos.Fusion:Component {

        
        pageTitle = Neos.Neos:Editable {
            property = 'title'
            @process.contentElementWrapping = Neos.Neos:ContentElementWrapping {
                @position = 'end'
            }
        }

        teaser = Neos.Neos:Editable {
            property = 'teaser_short'
            @process.contentElementWrapping = Neos.Neos:ContentElementWrapping {
                @position = 'end'
            }
        }

        renderer = afx`
            <main class="main">

                <div class="container grid md:grid-cols-2 lg:grid-cols-3 gridgap gridPadding">
                    <div class="lg:col-span-2">
                        <h1>{props.pageTitle}</h1>
                        <Neos.Fusion:Augmenter class="lead">
                            {props.teaser}
                        </Neos.Fusion:Augmenter>
                    </div>
                    <div class="hidden p-8 md:flex bg-base-100 box-h-1">
                        <div class="grid h-full grid-cols-1 place-content-between">
                            <h3 class="self-start">
                                { q(node).parent().find('[instanceof Abte.Site:Document.Kurs]').get() ? I18n.translate('Abte.Site:Main:currentCourses') : I18n.translate('Abte.Site:Main:noCourses') }
                                <br/><span class="text-primary-focus">
                                    { q(documentNode).parents().is('[uriPathSegment="humanmedizin"]') ? I18n.translate('Abte.Site:Main:solutionsForHum') : I18n.translate('Abte.Site:Main:solutionsForVet') }
                                </span></h3>
                            <div class="self-end">
                                <Abte.Site:Component.DropdownMenu startingPoint={q(documentNode).parent().get(0) }/>
                            </div>
                        </div>
                    </div>
                </div>

                <div class="bg-base-200">
                    <Abte.Site:Content.Container title="Kursanmeldung">
                        <Carbon.IncludeAssets:File file="resource://Onedrop.AjaxForm/Public/JavaScript/form.js" />
                        <Onedrop.AjaxForm:Form formIdentifier="kurs-form" />
                    </Abte.Site:Content.Container>
                </div>

                {props.main}
            </main>
              
        `
    }
}