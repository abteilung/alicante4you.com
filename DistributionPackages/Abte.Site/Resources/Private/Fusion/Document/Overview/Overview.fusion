prototype(Abte.Site:Document.Overview) < prototype(Abte.Site:Document.AbstractPage) {
    body.content = Neos.Fusion:Component {
        
        titleText = Neos.Fusion:Case {
            human {
                condition = ${ q(documentNode).parent().is('[uriPathSegment="humanmedizin"]') }
                renderer = ${ I18n.translate('Abte.Site:Main:humCoursesTitle') }
            }
            fallback {
                condition = ${ true }
                renderer = ${ I18n.translate('Abte.Site:Main:vetCoursesTitle') }
            }
        }

        subTitleText = Neos.Fusion:Case {
            human {
                condition = ${ q(documentNode).parent().is('[uriPathSegment="humanmedizin"]') }
                renderer = ${ I18n.translate('Abte.Site:Main:humCoursesSubTitle') }
            }
            fallback {
                condition = ${ true }
                renderer = ${ I18n.translate('Abte.Site:Main:vetCoursesSubTitle') }
            }
        }

        @context {
            titleText = ${ this.titleText }
            subTitleText = ${ this.subTitleText }
        }

        two = afx`
            <div class="grid h-full grid-cols-1 p-8 place-content-between">
                <h3 class="self-start">{titleText}<br/><span class="text-primary-focus">{subTitleText}</span></h3>
                <div class="self-end">
                    <Abte.Site:Component.DropdownMenu 
                        itemCollection={ q(node).parent().find('[instanceof Abte.Site:Document.Kurs]').get() ? q(node).parent().find('[instanceof Abte.Site:Document.Kurs]').get() : q(node).get() }
                        label={ q(node).parent().find('[instanceof Abte.Site:Document.Kurs]').get() ? I18n.translate('Abte.Site:Main:currentCourses') : I18n.translate('Abte.Site:Main:noCourses') }
                    />
                </div>
            </div>    
        `

        three = Abte.Site:Document.GridItemRenderer {
            node = ${ q(node).property('three') }
        }

        four = Abte.Site:Document.GridItemRenderer {
            node = ${ q(node).property('four') }
        }

        five = Abte.Site:Document.GridItemRenderer {
            node = ${ q(node).property('five') }
        }

        twoTheme = ${ q(documentNode).parent().is('[uriPathSegment="humanmedizin"]') ? 'human' : 'vet' }

        renderer = afx`
            <main class="main">
                <div class="container gridPadding-x">
                    <Abte.Site:Component.HeaderGrid 
                        oneTheme=''
                        two={props.two}
                        twoTheme={ props.twoTheme }
                        three={props.three}
                        threeTheme=''
                        four={props.four}
                        fourTheme='default'
                        fourClass="relative box-h-1 overflow-hidden"
                        five={props.five}
                        fiveTheme='default'
                        fiveClass="relative box-h-1 overflow-hidden"
                    />
                </div>
                {props.main}
            </main>
        `
    }
}


prototype(Abte.Site:Document.GridItemRenderer) < prototype(Neos.Fusion:Component) {

    node = ${ node }
    @context {
        node = ${ this.node }
    }

    renderer = Neos.Fusion:Case {
        isStory {
            condition = ${ q(node).is('[instanceof Abte.Site:Document.Kompetenz],[instanceof Abte.Site:Document.Story]') }
            renderer = Abte.Site:Component.ServiceCard
        }
        isProduct {
            condition = ${ q(node).is('[instanceof Abte.Site:Document.Product],[instanceof Abte.Site:Document.ProductLink]') }
            renderer = Abte.Site:Component.ImageCard {
                class = "relative w-full h-full group block"
                category=${ q( q(node).property('categories') ).property('title') }
            }
        }
        fallback {
            condition = ${ true }
            renderer = 'Not a Story'
        }
    }
}