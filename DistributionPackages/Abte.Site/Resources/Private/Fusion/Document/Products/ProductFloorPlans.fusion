prototype(Abte.Site:Component.ProductFloorPlans) < prototype(Neos.Neos:ContentComponent) {

    node = ${ node }

    @context {
        node = ${ this.node }
    }

    renderer = afx`
        <Abte.Site:Component.FloorPlanSlider />
    `
}



prototype(Abte.Site:Component.FloorPlanSlider) < prototype(Neos.Fusion:Component) {
    
    node = ${ node }
    images = ${ q(node).property('floorPlans') }

    @context {
        node = ${ this.node }
        images = ${ this.images }
    }

    productImages = Neos.Fusion:Loop {
        @if.hasImages = ${ images }
        items = ${ images }
        itemRenderer = Neos.Fusion:Tag {
            attributes {
                class = 'flex justify-center w-2/3 overflow-hidden box-shadow group lg:w-1/2 2xl:w-1/3 floorPlan roundedCorners'
            }
            content = Neos.Fusion:Augmenter {
                data-bp = Sitegeist.Kaleidoscope:AssetImageSource {
                    asset   = ${ item }
                }
                data-caption = ${ item.title }
                class = 'duration-150 scale-100 cursor-pointer hover:scale-105'
                content = Sitegeist.Kaleidoscope:Image {
                    imageSource = Sitegeist.Kaleidoscope:AssetImageSource {
                        asset   = ${ item }
                        width   = 800
                        height  = 800
                    }
                }
            }
        }
    }

    sliderConfig = Neos.Fusion:DataStructure {
        cellSelector = '.floorPlan'
        pageDots = false
        draggable = true
        // draggable = ${ node.context.inBackend ? false : true }
        prevNextButtons = true
        contain = true
        adaptiveHeight = true
        prevNextButtons = ${ images ? true : false }
        @process.stringify = ${ Json.stringify(value) }
    }
    
    renderer = afx`
        <div class="mb-4 md:mb-8 xl:mb-12 sliderRemoteContainer"
            data-aos="fade-in"
        >

            <div
                class="remoteSlider" 
                data-flickity={props.sliderConfig}
            >
                {props.productImages}
            </div>

        </div>    
    `
}