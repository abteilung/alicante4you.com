prototype(Abte.Site:Document.Product) < prototype(Abte.Site:Document.AbstractPage) {

    // Overriding Conditions from Jonnitto for Maps rendering
    head.googleMaps = Neos.Fusion:Tag {
        @if.render = ${ true }
    }

    body.javascripts {
        googleMapsFunction = Neos.Fusion:Tag {
            @if.render = ${ true }
        }
        googleMapsApi = Neos.Fusion:Tag {
            @if.render = ${ true }
        }
        googleMapsBackend = Neos.Fusion:Tag {
            @if.render = ${ true }
        }
    }



    body.content = Neos.Fusion:Component {

        filterItemsArray = ${ q(node).property('categories') ? q(node).property('categories') : null }
        @context {
            filterItemsArray = ${ this.filterItemsArray }
        }

        description = Neos.Neos:Editable {
            property = 'description'
            @process.contentElementWrapping = Neos.Neos:ContentElementWrapping {
                @position = 'end'
            }
        }
        content = Neos.Neos:ContentCollectionRenderer {
        @process.contentElementWrapping = Neos.Neos:ContentElementWrapping {
            @position = 'end'
        }
	}

        title = ${ q(node).property('title') }
        
        address = ${ q(node).property('address') }

        floorPlans = ${ q(node).property('floorPlans') }

        similarProducts = ${ q(node).property('categories') ? q(documentNode).parent().children('[instanceof Abte.Site:Mixin.Product]').filterByReference( Array.last( q(node).property('categories') ), 'categories' ).remove(documentNode).get() : null }

        features = Abte.Site:Component.ProductFeatures

        renderer = afx`

            <div class="space-y-12">
                <div class="container ">
                    <main class="mx-auto space-y-4 md:space-y-8 lg:w-2/3">

                        {props.main}

                        <Abte.Site:Content.Container 
                            id="id-features"
                            title={ I18n.translate('Abte.Site:Main:features') }
                            linkText=''
                            link=''
                            contentClass='productTable'
                        >
                            {props.features}
                        </Abte.Site:Content.Container>


                        <Abte.Site:Content.Container 
                            id="id-description"
                            title={ I18n.translate('Abte.Site:Main:description') }
                            linkText=''
                            link=''
                            contentClass='lead'
                        >
                            {props.description}
                        </Abte.Site:Content.Container>


                        <Abte.Site:Content.Container 
                            @if.hasFloorplans={props.floorPlans}
                            id="id-floor-plans"
                            title={ I18n.translate('Abte.Site:Main:floorPlans') }
                            linkText=''
                            link=''
                        >
                            <Abte.Site:Component.ProductFloorPlans />
                        </Abte.Site:Content.Container>


                        <Abte.Site:Content.Container 
                            id="id-location"
                            title={ I18n.translate('Abte.Site:Main:location') }
                            linkText=''
                            link=''
                        >
                            <Jonnitto.GoogleMaps:Map 
                                mapLocation={props.address}
                                zoom="12"
                                showInfo={true}
                                title={props.title}
                                showinfo="true"
                                maptype="terrain"
                            />
                        </Abte.Site:Content.Container>


                        <Abte.Site:Content.Container 
                            id="id-distances"
                            title={ I18n.translate('Abte.Site:Main:distances') }
                            linkText=''
                            link=''
                            @if.hasDistance={ node.context.inBackend || q(node).property('atm') || q(node).property('train') || q(node).property('bus') || q(node).property('postbox') || q(node).property('road') || q(node).property('school') }
                        >
                            <div class="grid grid-cols-3 md:grid-cols-5 gridgap">
                                <Abte.Site:Content.PropertyFeature textProperty='atm' featureIcon='atm' title={ I18n.translate('Abte.Site:Main:distATM') } />
                                <Abte.Site:Content.PropertyFeature textProperty='train' featureIcon='train' title={ I18n.translate('Abte.Site:Main:distRailway') } />
                                <Abte.Site:Content.PropertyFeature textProperty='bus' featureIcon='bus' title={ I18n.translate('Abte.Site:Main:distBus') } />
                                <Abte.Site:Content.PropertyFeature textProperty='postbox' featureIcon='postbox' title={ I18n.translate('Abte.Site:Main:distPost') } />
                                <Abte.Site:Content.PropertyFeature textProperty='road' featureIcon='road' title={ I18n.translate('Abte.Site:Main:distMotorway') } />
                                <Abte.Site:Content.PropertyFeature textProperty='school' featureIcon='school' title={ I18n.translate('Abte.Site:Main:distSchool') } />
                            </div>
                        </Abte.Site:Content.Container>

                    </main>


                    <Abte.Site:Content.Container 
                        @if.hasSimilarProducts={props.similarProducts}
                        title={ I18n.translate('Abte.Site:Main:similarProducts') }
                        linkText=''
                        link=''
                    >
                        <Abte.Site:Content.Navigation 
                            node={node}
                            navigationItems={props.similarProducts}
                            limit="3" 
                            layout="products" 
                        />
                    </Abte.Site:Content.Container>


                </div>
            </div>
        `

        @cache {
            mode = 'cached'
            entryIdentifier {
                node = ${node}
            }
            entryTags {
                1 = ${Neos.Caching.nodeTypeTag('Abte.Site:Document.Product', documentNode)}
                // 1 = ${Neos.Caching.nodeTag(node)}
                2 = ${Neos.Caching.nodeTag(filterItemsArray)}
                3 = ${Neos.Caching.descendantOfTag(filterItemsArray)}
            }
        }    
    }
}