prototype(Abte.Site:Component.ProductFilter) < prototype(Neos.Fusion:Component) {

  node = ${ node }
  @context {
    node = ${ this.node }
  }

  showAllFilter = false

  categoryItems = Neos.Fusion:Case {
    news {
      condition = ${ q(documentNode).is('[uriPathSegment="news-and-trends"]') }
      renderer = Neos.Neos:MenuItems {
        itemCollection = ${ q(documentNode).children('[instanceof Abte.Site:Document.Category]').get() }
        filter = ${ 'Abte.Site:Document.Category' }
      }
    }

    fallback {
      condition = ${ true }
      renderer = Neos.Neos:MenuItems {
        itemCollection = ${ q(site).children('[uriPathSegment="kategorien"]').children('[instanceof Abte.Site:Document.Category]').get() }
        filter = ${ 'Abte.Site:Document.Category' }
      }
    }
      
  }

  itemsWrapClass = Neos.Fusion:Case {
      projects {
        condition = ${ q(node).is('[instanceof Abte.Site:Document.Projects]') }
        renderer = 'grid sm:grid-cols-2 md:grid-cols-3 xl:grid-cols-3 mt-12 filterContent w-full relative gridgap'
      }
      fallback {
        condition = ${ true }
        // renderer = 'mt-12 filterContent flex-wrap flex'
        renderer = 'grid sm:grid-cols-2 md:grid-cols-3 xl:grid-cols-3 mt-12 filterContent w-full relative gridgap'
      }
  }

  filterItems = Neos.Fusion:Loop {
    items = ${ q(documentNode).children('[instanceof Abte.Site:Mixin.Product]').get() || q(documentNode).find('[instanceof Abte.Site:Document.Story]').sort('datePublished', 'DESC').get() }
    itemName = 'node'
    iterationName = 'i'
    
    itemRenderer = Neos.Fusion:Case {
      productCard {
        condition = ${ q(node).is('[instanceof Abte.Site:Document.Product]') }
        renderer = Abte.Site:Component.ImageCard {
          node = ${ q(node) }
        }
      }
      newsCard {
        condition = ${ q(node).is('[instanceof Abte.Site:Document.Story]') }
        renderer = Abte.Site:Component.ServiceCard {
          node = ${ q(node) }
        }
      }
      fallback {
        condition = ${ true }
        renderer = Abte.Site:Component.NewsCard {
          node = ${ q(node) }
        }
      }
      
    }
      
    // itemRenderer.@if.isWithinLimitsOfFour= ${ i.cycle <= 4 ? true : false }
    // @process.removeDuplicates = ${Array.unique(value)}
  }

  renderer = afx`
    <div 
      id="filterContainer" 
      x-data="{cat: window.location.hash ? window.location.hash.replace('#', '') : 'all' }"
    >

      <div class=" filter gridPadding-x gridPadding-bottom">

        <!-- Dropdown menu on small screens -->
        <div class="lg:hidden">
          <label for="filter-tab" 
            class="sr-only">Select a tab</label>
          <select 
            x-model="cat"
            name="filter-tab" 
            class="block w-full py-2 pl-3 pr-10 text-base border-2 rounded-md text-primary focus:outline-none focus:ring-transparent border-base-100 focus:border-base-100 sm:text-sm"
          >
            <option value="all"
                @if.showAllFilter={props.showAllFilter}
            >{ I18n.translate('Abte.Site:Main:all') }</option>
            <Neos.Fusion:Loop 
                items={props.categoryItems} 
                iterationName='i'
                @if.hasCats={props.categoryItems}
            >
              <Abte.Site:Component.filterOption node={item.node} />
            </Neos.Fusion:Loop>
          </select>
        </div>


        <!-- Tabs at lg breakpoint and up -->
        <div class="container hidden lg:block">
          <nav 
            class="flex space-x-6 border-b-2 h3 border-base-200 mixitupFilter" 
            aria-label="Tabs"
            
          >
            <button
              @if.showAllFilter={props.showAllFilter}
              type="button"
              id="filterAll"
              "@click"="cat = 'all'" 
              ":class"="{'!text-primary': cat === 'all'}"
              class="cursor-pointer filterButton text-base-300 pb-2 mb-[-2px]"
            >
                { I18n.translate('Abte.Site:Main:all') }
            </button>

            <Neos.Fusion:Loop 
              items={props.categoryItems} 
              iterationName='i'
              @if.hasCats={props.categoryItems}
            >

              <div
                  x-data="{ show: false }"
                  x-cloak
                  class="relative"
              >
                <div 
                    "@mouseover"="show = true"
                    "@mouseout"="show = !true"
                    class="relative filter-box group"
                >
                  <div class="flex">
                    <Abte.Site:Component.filterButton 
                        node={item.node} 
                        content={q(item.node).property('title')}
                    />

                    <Abte.Site:Component.navCaret 
                      @if.hasSubsOfTypeCate={ q(item.node).children('[instanceof Abte.Site:Document.Category]').count() > 0 ? true : false }
                    />
                  </div>

                  <div 
                    @if.hasSubsOfTypeCate={ q(item.node).children('[instanceof Abte.Site:Document.Category]').count() > 0 ? true : false }
                    x-show="show"
                    "@click.away"="show = false" 
                    x-transition:enter="transition duration-300"
                    x-transition:enter-start="opacity-0"
                    x-transition:enter-end="opacity-100"
                    x-transition:leave="transition duration-300"
                    x-transition:leave-start="opacity-100"
                    x-transition:leave-end="opacity-0"
                    class={props.subMenuOrientation + ' ' + ' absolute shadow-lg text-base left-0 z-20 w-auto origin-top-left bg-white p-6 pt-4 pb-3'}
                  >
                    <div role="none">
                      <Neos.Fusion:Loop items={item.subItems}>
                        <Abte.Site:Component.filterButton 
                          node={item.node} 
                          content={q(item.node).property('title')}
                        />
                      </Neos.Fusion:Loop>
                    </div>
                  </div>
            
                </div>
              </div>

            </Neos.Fusion:Loop>

          </nav>
        </div>
        <div class={props.itemsWrapClass}>
          {props.filterItems}
        </div>
      </div>
    </div>
  `

}

prototype(Abte.Site:Component.Navigation.Filter) < prototype(Neos.Fusion:Component) {

    filterItemsFromCategories = Neos.Neos:MenuItems {
        itemCollection = ${ q(node).children('[instanceof Abte.Site:Document.Category]') ? q(node).children('[instanceof Abte.Site:Document.Category]').get() : null }
        filter = ${ 'Abte.Site:Document.Category' }
    }

    renderer = afx`
        <div 
            id="filterOptions" 
            class="flex justify-between block py-4 mb-12 responsiveMargin"
        >
            <div class="container z-20 flex pb-4 space-x-6 text-left border-b-2 border-base-100">
                <div class="flex space-x-6">
                    
                    <Abte.Site:Component.filterMenuSection 
                        filterName="kate"
                        menuItems={props.filterItemsFromCategories} 
                    />
                    
                </div>
            </div>
		</div>
    `
}


prototype(Abte.Site:Component.navCaret) < prototype(Neos.Fusion:Component) {
    renderer = afx`
        <svg
            width="16"
            class={'mt-1 ml-1 -rotate-90 text-navigation text-base-300 filter-arrow group-hover:text-primary'}
            height="16"
            viewBox="0 0 24 24"
            fill="none"
            xmlns="http://www.w3.org/2000/svg"
            >
            <path
                d="M16.2426 6.34317L14.8284 4.92896L7.75739 12L14.8285 19.0711L16.2427 17.6569L10.5858 12L16.2426 6.34317Z"
                fill="currentColor"
            />
        </svg>
    `
}

