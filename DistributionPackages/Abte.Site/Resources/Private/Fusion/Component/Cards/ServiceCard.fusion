prototype(Abte.Site:Component.ServiceCard) < prototype(Neos.Neos:ContentComponent) {

    node = ${ node }
    @context {
        node = ${ this.node }
    }

    imageSource = Sitegeist.Kaleidoscope:AssetImageSource {
        asset   = ${ q(node).property('teaserImage') ? q(node).property('teaserImage') : q(node).property('image') }
        width   = 640
        height  = 640
    }

    title = ${ q(node).property('title') }
    subTitle = ${ q(node).property('subTitle') }

    icon = Abte.Site:Icon.ArrowRight {
        class = 'absolute z-10 text-white duration-150 scale-75 bottom-6 lg:scale-100 group-hover:right-6 right-8'
    }

    class = 'group relative h-full w-full block'
    imageClass = 'w-full h-full object-cover object-center duration-300 scale-100 group-hover:scale-105'

    pageTitleForFilter = ${ q(documentNode).property('title') }
    kates = Abte.Site:Component.FilterKates

    datePublished = ${ Date.formatCldr( q(node).property('datePublished'), 'd. MMM y', 'de_CH') }


    renderer = afx`

        <Neos.Neos:NodeLink 
            node={ q(node).get(0) }
            attributes.class={ props.class }
            attributes.x-show={ props.kates }
            attributes.x-show.@if.isOfType={ q(documentNode).is('[instanceof Alicante.MixitUp:Include]') }
            attributes.x-transition:enter="transition duration-50 ease-out"
            attributes.x-transition:enter-end="scale-100 opacity-100"
            attributes.x-transition:enter-start="scale-0 opacity-0"
            attributes.x-transition:leave="transition duration-50 ease-in"
            attributes.x-transition:leave-end="opacity-0 scale-100"
        >
            <div class="absolute block z-10 grid content-end w-full h-full p-6 text-white md:p-4 lg:p-8 serviceCard">
                <p>{ q(documentNode).is('[uriPathSegment="news-and-trends"],[uriPathSegment="home"]') ? props.datePublished : props.title }</p>
                <h3 class="pr-12 mb-0 line-clamp-none md:line-clamp-3 lg:line-clamp-none">{ q(node).property('titleOverride') ? q(node).property('titleOverride') : q(documentNode).is('[instanceof Abte.Site:Document.Kompetenzen],[instanceof Abte.Site:Document.Products]') ? q(node).property('subTitle') : q(node).property('title') }</h3>
            </div>
            {props.icon}

            <div class="relative h-full overflow-hidden bg-base-100">
                <Sitegeist.Lazybones:Image 
                    @if.hasImage={props.imageSource}
                    imageSource={props.imageSource} 
                    class={props.imageClass}
                />
            </div>
        </Neos.Neos:NodeLink>
    `
}