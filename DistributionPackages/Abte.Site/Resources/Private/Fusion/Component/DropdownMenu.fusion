prototype(Abte.Site:Component.DropdownMenu) < prototype(Neos.Fusion:Component) {

    node = ${ node }
    @context {
        node = ${ this.node }
    }

    label = ${ q(node).property('titleOverride') }

    class = ''

    startingPoint = ${ q(node).property('startingPoint') }
    itemCollection = ${ q(node).property('itemCollection') }
    layout = ${ layout }

    @context {
        startingPoint = ${ this.startingPoint }
        itemCollection = ${ this.itemCollection }
        layout = ${ this.layout }
    }

	menuItems = Neos.Neos:MenuItems {
        startingPoint = ${ startingPoint }
        startingPoint.@if.hasStartingpoint = ${ startingPoint }

        itemCollection = ${ itemCollection }
        itemCollection.@if.navigationItems = ${ itemCollection }

        renderHiddenInIndex = ${ itemCollection ? true : false }

        filter = '!Alicante.Navigation:Document.MegaMenu, !Neos.Neos:ContentCollection, !Abte.Site:Document.Category'
    }


    renderer = afx`
        <div
            x-cloak
            class={props.class}
            x-data="{ open: false }"
        >

            <div class="relative mt-1">
                <button 
                    "@click"="open = !open"
                    "@click.away"="open = false" 
                    type="button"
                    class="group relative w-full py-3 pl-5 pr-10 text-left bg-white rounded-full cursor-default focus:outline-none focus:ring-0 focus:ring-primary focus:border-primary-focus sm:text-sm cursor-pointer"
                    aria-haspopup="listbox"
                    aria-expanded="true"
                    aria-labelledby="listbox-label">
                    <span class="block truncate text-primary">
                        {props.label}
                    </span>
                    <span class="absolute inset-y-0 right-0 flex items-center pr-2 pointer-events-none">
                        <Alicante.Navigation:Component.SubNavCaret class="w-5 h-5 text-accent transition group-hover:scale-125"/>
                    </span>
                </button>
          
                <ul 
                    x-show="open"
                    "@click.away"="open = false" 
                    x-transition:enter="transition duration-300"
                    x-transition:enter-start="opacity-0"
                    x-transition:enter-end="opacity-100"
                    x-transition:leave="transition duration-300"
                    x-transition:leave-start="opacity-100"
                    x-transition:leave-end="opacity-0"
                    class="absolute z-10 w-full py-1 mt-1 overflow-auto text-base bg-white shadow-lg rounded-2xl max-h-60 ring-1 ring-primary ring-opacity-5 focus:outline-none sm:text-sm" tabindex="-1" role="listbox" aria-labelledby="listbox-label" aria-activedescendant="listbox-option-3">
                    <Neos.Fusion:Loop items={props.menuItems}>
                        <li class="relative py-2 pl-3 cursor-default select-none text-primary hover:text-primary-focus pr-9" id="listbox-option-0" role="option">
                            <Carbon.Link:Link 
                                node={item.node}
                                attributes.class="block font-normal truncate"
                            >
                                {item.label}
                            </Carbon.Link:Link>
                        </li>
                    </Neos.Fusion:Loop>

                </ul>
            </div>
        </div>

    `

}