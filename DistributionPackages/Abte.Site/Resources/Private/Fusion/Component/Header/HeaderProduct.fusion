prototype(Abte.Site:Component.HeaderProduct) < prototype(Neos.Fusion:Component) {

    pageTitle = Neos.Neos:Editable {
        property = 'title'
        block = ${ true }
        @process.contentElementWrapping = Neos.Neos:ContentElementWrapping {
            @position = 'end'
        }
    }

    address = Neos.Neos:Editable {
        property = 'address'
        block = ${ true }
        @process.contentElementWrapping = Neos.Neos:ContentElementWrapping {
            @position = 'end'
        }
    }

    subTitle = Neos.Neos:Editable {
        property = 'subTitle'
        block = ${ false }
        node = Neos.Fusion:Case {
            isReference {
                condition = ${ q(documentNode).is('[instanceof Abte.Site:Document.ProductLink]') }
                renderer = ${ q(node).property('reference')}
            }
            fallback {
                condition = ${ true }
                renderer = ${ node }
            }
        }
        @process.contentElementWrapping = Neos.Neos:ContentElementWrapping {
            @position = 'end'
        }
    }

    imageSource = Sitegeist.Kaleidoscope:AssetImageSource {
        asset   = ${ q(node).closest("[instanceof Neos.Neos:Document][headerImage][headerImage != '']").property('headerImage') || q(node).closest("[instanceof Neos.Neos:Document][image][image != '']").property('image') }
        height  = 480
        width   = 2200
    }

    price = ${ q(node).property('productPrice')}

    category = ${ q( q(node).property('categories') ).property('title') }


    renderer = afx`

        <div 
            class="h-[175px] md:h-[275px] xl:h-[300px] 2xl:h-[350px] overflow-hidden"
            style={'background: url(' + props.imageSource + '); background-size: cover; background-position: center center;'}
        >
        </div>

        <div class="container py-4 gridPadding-x md:py-8 lg:py-12 2xl:py-24">
            <div class="w-full mx-auto space-y-4 lg:w-2/3 md:space-y-8">
                <p>{props.category}</p>
                <h1>
                    <span class="productTitle">{props.pageTitle}</span>
                </h1>
                <p class="lead">{props.address}</p>
            </div>
        </div>
        <Abte.Site:Component.ProductSlider />
    `

	@cache {
		mode = 'cached'
		entryIdentifier {
			documentNode = ${documentNode}
		}
		entryTags {
			1 = ${Neos.Caching.nodeTypeTag('Neos.Neos:Document', documentNode)}
		}
	}
}
