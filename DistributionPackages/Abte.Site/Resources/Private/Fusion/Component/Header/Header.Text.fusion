prototype(Abte.Site:Component.TextHeader) < prototype(Neos.Fusion:Component) {
    
    node = ${ node }
    @context {
        node = ${ this.node }
    }

    pageTitle = Neos.Neos:Editable {
        property = 'titleOverride'
        node = ${ node }
        @process.contentElementWrapping = Neos.Neos:ContentElementWrapping {
            @position = 'end'
        }
    }

    subTitle = Neos.Neos:Editable {
        property = 'subTitle'
        node = ${ node }
        @process.contentElementWrapping = Neos.Neos:ContentElementWrapping {
            @position = 'end'
        }
    }

    imageSource = Sitegeist.Kaleidoscope:AssetImageSource {
        asset   = ${ q(documentNode).find("[instanceof Abte.Site:Document.Story][image][image != '']").property('image') || q(node).closest("[instanceof Neos.Neos:Document][image][image != '']").property('image') }
        height  = 480
        width   = 2200
    }

    hideHeader = ${ q(node).property('hideHeader') }


    renderer = afx`

        <div 
            @if.hideHeader={props.hideHeader ? false : true}
            class="flex items-center mb-4 md:mb-12 min-h-[175px] md:min-h-[275px] xl:min-h-[300px] overflow-hidden gridPadding-x py-4 md:py-8 lg:py-12 2xl-py-24"
            style={'background: url(' + props.imageSource + '); background-size: cover; background-position: center center;'}
        >

                <div class="container">
                    <div>
                        <Neos.Fusion:Augmenter class="text-white lead">
                            {props.subTitle}
                        </Neos.Fusion:Augmenter>
                    </div>
                    <div>
                        <h1 class="text-white">{props.pageTitle}</h1>
                    </div>
                </div>

        </div>

    `

	@cache {
		mode = 'cached'
		entryIdentifier {
			documentNode = ${documentNode}
		}
		entryTags {
			1 = ${Neos.Caching.nodeTypeTag('Neos.Neos:Document', documentNode)}
		}
	}
}
