
prototype(Abte.Site:Content.Container) < prototype(Neos.Neos:ContentComponent) {

    node = ${ node }
    @context {
        node = ${ this.node }
    }

    class = 'gridPadding'

    fullWidth = ${ q(node).property('fullWidth') }
    
    contentFullWidth = ${ q(node).property('contentFullWidth') ? null : 'container' }

    fullWidthClass = ${ this.fullWidth ? 'cnt-container' : 'container' }
    
    dropShadow = ${ q(node).property('dropShadow') }
    
    removePadding = ${ q(node).property('removePadding') ? '!py-0' : '' }

    bgColor = Abte.Site:Mixin.BgColor
    
    shadowClass = ${ (this.dropShadow ? 'box-shadow p-8' : null ) + ' ' + this.bgColor }

    title = Neos.Neos:Editable {
        property = 'title'
        node = ${ node}
    }

    subTitle = Neos.Neos:Editable {
        property = 'subTitle'
        node = ${ node}
    }
    
    link = ${ q(node).property('link') }

    linkText = Neos.Neos:Editable {
        property = 'linkText'
        node = ${ node}
        block = ${ false }
    }

	content = Neos.Neos:ContentCollectionRenderer {
        @process.contentElementWrapping = Neos.Neos:ContentElementWrapping {
            @position = 'end'
        }
	}

  	renderer = afx`
        <div 
            id={ 'id-' + q(node).property('_nodeName') }
            class={props.class + ' ' + props.removePadding + ' ' + props.shadowClass + ' ' + props.fullWidthClass}
        >
            
            <div class={props.contentFullWidth}>
                <div class="flex items-start justify-between mb-4 md:mb-8 lg:mb-12 containerHeader"
                    @if.hasTitleOrLink={ props.title ? true : props.link ? true : false }
                >
                    <h2>{props.title}</h2>
                    <Abte.Site:Content.Button 
                        text={props.linkText} 
                        target={props.link} 
                        @if.hasLinkText={props.linkText}
                    />
                </div>
                
                <div>
                    {props.content}
                </div>
            </div>
            
        </div>
    `
}
